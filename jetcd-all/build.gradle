/*
 * Copyright 2016-2020 The jetcd authors
 *
 * Licensed under the Apache License, Version 2.0 (the 'License');
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an 'AS IS' BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "com.github.johnrengelman.shadow" version "6.0.0"
}

dependencies {
    implementation project(':jetcd-common')
    implementation project(':jetcd-core')
    implementation project(':jetcd-extensions')
    implementation project(':jetcd-resolver')
    implementation project(':jetcd-resolver-dns-srv')
}

// disable standard jar task so we can have a single ube jar as result
jar.enabled = false

task shadedSourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from {
        rootProject.subprojects.collect { it.sourceSets.main.allSource }
    }
}

task shadedJavadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from {
        rootProject.subprojects.collect { it.javadoc.destinationDir }
    }
}

shadowJar {
    dependsOn shadedSourcesJar, shadedJavadocJar

    relocate 'javax.annotation', 'io.etcd.jetcd.shaded.javax.annotation'
    relocate 'com.google', 'io.etcd.jetcd.shaded.com.google'
    relocate 'io.grpc', 'io.etcd.jetcd.shaded.io.grpc'
    relocate 'io.netty', 'io.etcd.jetcd.shaded.io.netty'
    relocate 'io.opencensus', 'io.etcd.jetcd.shaded.io.opencensus'
    relocate 'net.jodah', 'io.etcd.jetcd.shaded.net.jodah'

    dependencies {
        exclude(dependency('org.slf4j:slf4j-api'))
        exclude(dependency('com.google.errorprone:.*'))
        exclude(dependency('com.google.code.findbugs:.*'))
        exclude(dependency('com.google.j2objc:.*'))
        exclude(dependency('org.codehaus.mojo:.*'))
        exclude(dependency('org.checkerframework:.*'))
        exclude(dependency('com.github.spotbugs:.*'))
    }

    mergeServiceFiles()

    classifier = null
}

artifacts {
    archives shadedSourcesJar, shadedJavadocJar
}

